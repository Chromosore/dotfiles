#!/usr/bin/env fish

set script (basename (status current-filename) .fish)
set options 'h/help' 'p/prune' 'i/install-root=' 'v/verbose' 'f/force'
argparse -n $script $options -- $argv

set help "$script [options] package

Remove an installed Mac OS X Installer flat package

-p, --prune                 Remove directories left empty after uninstalling the package
-i DIR, --install-root DIR  Work with packages installed in the volume DIR instead of /
-f, --force                 Do not prompt before proceeding"

if set -q _flag_help
	echo $help
	exit 0
end



set package $argv[1]

if test -z "$package"
	echo $help >&2
	exit 1
end


set -q _flag_install_root
and set root (realpath -s $_flag_install_root)
or set root ''


if not pkgutil --volume $root/ --pkg-info $package &>/dev/null
	test (string sub -s -4 -- "$package") = ".pkg"
	or set package {$package}.pkg
end

if not pkgutil --volume $root/ --pkg-info $package &>/dev/null
	echo  "No such package: '$package'" >&2
	exit 1
end


if set -q _flag_verbose
	function verbose
		echo $argv
	end
else
	function verbose
	end
end



if not set -q _flag_force
	read answer -P "Remove '$package'? [y/N] "
	test (string lower "$answer") = y
	or exit 1
end

set counter 0
pkgutil --volume $root/ --only-files --files $package |
while read file
	set file $root/$file
	if test -f $file
		sudo rm -f $file
		and verbose "Removed $file"
		and set counter (math $counter + 1)
		or exit 1
	end
end
echo "Successfully removed $counter files"


set -q _flag_prune
and if not set -q _flag_force
	read answer -P "Prune empty directories? [y/N] "
	test (string lower "$answer") = y
	or set -e _flag_prune
end

if set -q _flag_prune
	printf '%s\n' $files | string replace -r '/[^/]+/*$' '' | sort | uniq |
	while read dir
		if test -d $dir
			sudo rmdir -p $dir 2>/dev/null
			and verbose "Removed $dir"
		end
	end
	echo "Successfully pruned remaining empty directories"
end


if not set -q _flag_force
	read answer -P "Forget '$package'? [y/N] "
	test (string lower "$answer") = y
	or exit 1
end

sudo pkgutil --volume $root/ --forget $package
